plugins {
  id 'java-library'
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '3.0.2'
  id 'org.owasp.dependencycheck' version '8.0.1'
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'org.sonarqube' version '3.5.0.2730'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
  group = "build"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  group = "Verification"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

tasks.withType(Checkstyle).each {task -> configure(task) {
  group = "checkstyle"
}}

pmd {
  toolVersion = "6.41.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

tasks.withType(Pmd).each {task -> configure(task) {
  group = "pmd"
}}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "SSCS HMC Stub"
    property "sonar.projectKey", "sscs-hmc-stub"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = 0
  suppressionFile = 'config/owasp/suppressions.xml'

  cveValidForHours = 24

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
  }

  skipConfigurations = [
    "checkstyle",
    "compileOnly",
    "pmd",
    "integrationTest",
    "functionalTest",
    "smokeTest",
    "contractTestRuntimeClasspath",
    "contractTestCompileClasspath"
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext['snakeyaml.version'] = '1.33'

dependencies {

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version: '2.7.8'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter', version: '4.0.0'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.0.0'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.0.0'

  implementation group: 'com.github.hmcts', name: 'sscs-common', version: '4.24.3', {
    exclude group: 'org.springdoc'
  }

  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '2.2.0'
  // https://mvnrepository.com/artifact/jakarta.jms/jakarta.jms-api
  implementation group: 'jakarta.jms', name: 'jakarta.jms-api', version: '3.1.0'
  // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
  implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.2'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '5.1.9'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.19.0'

  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.5'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.5'

  implementation group: 'io.rest-assured', name: 'rest-assured', version: '5.3.0'

  implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

  compileOnly group: 'org.projectlombok', name: 'lombok'

  annotationProcessor group: 'org.projectlombok', name: 'lombok'

  testImplementation(platform(group: 'org.junit', name: 'junit-bom', version: '5.9.2'))
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

dependencyManagement {
  imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:2022.0.0"
  }
  dependencies {
    // CVE-2022-38752
    dependencySet(group: 'org.yaml', version: '1.33') {
      entry 'snakeyaml'
    }
  }
}

mainClassName = 'uk.gov.hmcts.reform.sscs.Application'

bootJar {
  archiveFileName = "sscs-hmc-stub.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
